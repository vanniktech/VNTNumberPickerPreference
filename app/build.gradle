apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        applicationId "com.vanniktech.vntnumberpickerpreference.sample"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0.0"
    }

    def shouldSign = hasProperty('RELEASE_STORE_FILE') && hasProperty('RELEASE_STORE_PASSWORD') && hasProperty('RELEASE_KEY_ALIAS') && hasProperty('RELEASE_KEY_PASSWORD')

    if (shouldSign) {
        signingConfigs {
            release {
                // http://stackoverflow.com/a/21020469/1979703
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            shrinkResources true
            zipAlignEnabled true

            if (shouldSign) {
                signingConfig signingConfigs.release
            }

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
                }
            }
        }

        debug {
            versionNameSuffix '-RC'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':library')
}